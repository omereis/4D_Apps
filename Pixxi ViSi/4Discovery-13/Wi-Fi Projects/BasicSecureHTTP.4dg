#platform "pixxiLCD-13P2CT-CLB"

#inherit "4DGL_16bitColours.fnc"

#inherit "ESP8266_4DGL_Flash.inc"

// Contains the Wi-Fi network SSID and Password
#inherit "ESP_WiFi_Network.inc"
// Edit this file to match your Wi-Fi network
// The changes will be used for all other examples

#DATA
    byte _4dprint       0x92, 0xe3, 0x17, 0xb2, 0x5e, 0x29, 0x53, 0x7e, 0x58, 0x65,
                        0xc6, 0xc1, 0xf6, 0x20, 0x3e, 0x0b, 0xd5, 0xe6, 0xce, 0xb5
#END

func main()

    gfx_ScreenMode(PORTRAIT);

    media_Init();
    ESP_Initialize(ESP_4DISCOVERY13);

    WiFi_SetMode(WIFI_STATION);

    gfx_MoveTo(0, 95);
    print("Connecting to ", [STR]SSID);
    WiFi_Begin(SSID, PASSWORD);
    while(WiFi_Status() != WIFI_CONNECTED)
        pause(500);
        putch('.');
    wend
    putch('\n');
    WiFi_PrintLocalIP(0);
    putch('\n');

    // HTTP_SetSecure(0); // Setting the thumbprint to 0 allows 
    // HTTPS requests without checking the thumbprint (not advisable)
    HTTP_SetSecure(_4dprint);

    HTTP_SetMode(HTTP_GET);
    HTTP_SetPort(443);
    HTTP_SetOutputSector(0, 0);

    HTTP_SetHost("www.4dsystems.com.au");
    HTTP_SetPath("/4Discovery/4dsystems.txt");
    print("Requesting 4dsystems.txt\n");
    HTTP_StartRequest(1);

    print("Done\n\nContents:\n\n");

    var file, fileBuffer[100];
    file := file_Open("output.txt", 'r');
    file_Read(fileBuffer, 200, file);
    print([STR]fileBuffer);

    repeat
    forever

endfunc

